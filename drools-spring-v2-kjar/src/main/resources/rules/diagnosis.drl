package rules;

import drools.model.*;
import java.util.*;

agenda-group "bolesti"

rule "Prehlada"
	no-loop
	lock-on-active true
	salience ($sdc.getSymptomsSatisfied()/$sdc.getSymptomsNumber()*100)
	when
		$d : Disease( disease == "Prehlada"  )
		$ex : Examination(disease == null , $symptoms: symptoms)
		$sdc : Salience( $s : symptomsSatisfied >=4 ) from accumulate(
			$symptom : Symptom (
				text == "Curenje iz nosa" ||
				text == "Bol u grlu" ||
				text == "Glavobolja" ||
				text == "Kijanje" ||
				text == "Kasalj"
			) from $symptoms,
            init(Salience $sal = new Salience(5);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
            )
	then
		modify($ex) {setDisease($d);}
		System.out.println("Prehlada: Zadovoljenih simptoma " + $sdc.getSymptomsSatisfied() + ".");
end

rule "Groznica"
	no-loop
	lock-on-active true
	salience ($sdc.getSymptomsSatisfied()/$sdc.getSymptomsNumber()*100)
	when
		$d : Disease( disease == "Groznica" )
		$ex : Examination(disease == null , $symptoms: symptoms)
		$sdc : Salience( $s : symptomsSatisfied >=4 ) from accumulate(
			$symptom : Symptom (
				text == "Curenje iz nosa" ||
				text == "Bol u grlu" ||
				text == "Glavobolja" ||
				text == "Kijanje" ||
				text == "Kasalj" ||
				text == "Drhtavica"||
				text == "Temperatura veca od 38"
			) from $symptoms,
            init(Salience $sal = new Salience(7);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
		)
	then
		modify($ex) {setDisease($d);}
        System.out.println("Groznica: Zadovoljenih simptoma " + $sdc.getSymptomsSatisfied() + ".");
end


rule "Upala krajnika"
	no-loop
	lock-on-active true
	salience ($sdc.getSymptomsSatisfied()/$sdc.getSymptomsNumber()*100)
	when
		$d : Disease( disease == "Upala krajnika" )
		$ex : Examination(disease == null , $symptoms: symptoms)
		$sdc : Salience( $s : symptomsSatisfied >=4 ) from accumulate(
			$symptom : Symptom (
				text == "Bol u grlu" ||
				text == "Glavobolja" ||
				text == "Drhtavica" ||
				text == "Temperatura od 40 do 41" ||
				text == "Bol koji se siri do usiju" ||
				text == "Gubitak apetita" ||
				text == "Umor" ||
				text == "Zuti sekret iz nosa"
			) from $symptoms,
            init(Salience $sal = new Salience(8);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
		)
	then
		modify($ex) {setDisease($d);}
        System.out.println("Upala krajnika: Zadovoljenih simptoma " + $sdc.getSymptomsSatisfied() + ".");
end

rule "Sinusna infekcija"
	no-loop
	lock-on-active true
	salience ($sdc.getSymptomsSatisfied()/$sdc.getSymptomsNumber()*100)
	when
	    $dc : DateChecker()
		$d : Disease( disease == "Sinusna infekcija" )
		$ex : Examination(disease == null , $symptoms: symptoms)
		$patient: Patient($examinations : examinations)
		$sdc : Salience( $s : symptomsSatisfied >=3 ) from accumulate(
			$symptom : Symptom (
				text == "Oticanje oko ociju" ||
				text == "Glavobolja" ||
				text == "Zuti sekret iz nosa" ||
				text == "Bol u grlu" ||
				text == "Temperatura veca od 38" ||
				text == "Kasalj"
			) from $symptoms,
            init(Salience $sal = new Salience(8);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
		)

		Number(intValue >= 1) from accumulate (
        $oldDiagnose: Examination( disease.disease=="Prehlada" || disease.disease=="Groznica", $dc.last60Days()<date ) from $examinations,
        init(int count = 0;),
        action(count++;
        ),
        result(count)
        )
	then
		modify($ex) {setDisease($d);}
        System.out.println("Sinusna infekcija: Zadovoljenih simptoma " + $sdc.getSymptomsSatisfied() + ".");
end

rule "Dijabetes"
	no-loop
	lock-on-active true
	salience 100
	when
		$d : Disease( disease == "Dijabetes" )
		$ex : Examination(disease == null , $symptoms: symptoms)
		$sdc : Salience( $s : allSymptomsSatisfied == true ) from accumulate(
			$symptom : Symptom (
				text == "Cesto uriniranje"	||
                text == "Gubitak tjelesne tezine" 	||
                text == "Zamor"	 ||
                text == "Mucnina i povracanje"
			) from $symptoms,
            init(Salience $sal = new Salience(4);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
		)
	then
		modify($ex) {setDisease($d);}
        System.out.println("Dijabetes: Svi simptomi zadovoljeni.");
end


rule "Hipertenzija"
	no-loop
	lock-on-active
	salience 20
	when
		$dc : DateChecker()
		$d: Disease( disease == "Hipertenzija")
		$patient: Patient($examinations : examinations)
		$ex : Examination(disease == null , $symptoms: symptoms)
		$oldSymptoms: List() from accumulate(
			Examination($dc.last6Months()<date, $currSymptoms: symptoms) from $examinations,
			init(List symptomList = new ArrayList<>();),
			action(symptomList.addAll($currSymptoms);)
			result(symptomList)
		)


		$symptomNum: Number(intValue >= 10) from accumulate(
			Symptom(text == "Visok krvni pritisak") from $oldSymptoms,
			init(int count = 0;),
			action(count++;),
			result(count)
		)

	then
		modify($ex) {setDisease($d);}
		System.out.println("Hipertenzija: Svi simptomi zadovoljeni.");
end


rule "Hronicna bubrezna bolest"
	no-loop
	lock-on-active
	salience 20
	when
		$dc : DateChecker()
		$d: Disease( disease == "Hronicna bubrezna bolest")
		$patient: Patient($examinations : examinations)
		$hypertension: Disease(disease == "Hipertenzija")
		$diabetes: Disease(disease == "Dijabetes")
		$ex : Examination(disease == null , $symptoms: symptoms)
		$sdc : Salience( $s : symptomsSatisfied >=2 ) from accumulate(
            $symptom : Symptom (
            	text == "Zamor"	||
                text == "Nocturia" 	||
                text == "Otoci nogu i zglobova"	 ||
                text == "Gusenje" ||
                text == "Bol u grudima"
            ) from $symptoms,
            init(Salience $sal = new Salience(4);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
            )
            and
            (
            Number(intValue >= 1) from accumulate(
            Examination($dc.last6Months()>date, disease.disease == $hypertension.disease) from $examinations,
            init(int count = 0;),
            action(count++;),
            result(count)
            )
            or
            Number(intValue >= 1) from accumulate(
            Examination(disease.disease == $diabetes.disease) from $examinations,
            init(int count = 0;),
            action(count++;),
            result(count)
            )
            )
	then
		modify($ex) {setDisease($d);}
		System.out.println("Hronicna bubrezna bolest: Svi simptomi zadovoljeni.");
end


rule "Akutna bubrezna povreda"
	no-loop
	lock-on-active
	salience 20
	when
		$dc : DateChecker()
		$d: Disease( disease == "Akutna bubrezna povreda")
		$patient: Patient($examinations : examinations)
		$ex : Examination(disease == null , $symptoms: symptoms)
		$sdc : Salience( $s : symptomsSatisfied >=2 ) from accumulate(
            $symptom : Symptom (
            	text == "Zamor"	||
                text == "Gusenje" 	||
                text == "Otoci nogu i zglobova"	 ||
                text == "Dijareja"
            ) from $symptoms,
            init(Salience $sal = new Salience(4);),
            action($sal.increaseSatisfiedSymptoms();),
            result($sal)
            )
            and
            (
             (
              Number(intValue >= 1) from accumulate(
                  Symptom(text == "Oporavak od operacije") from $symptoms,
                  init(int count = 0;),
                  action(count++;),
                  reverse(count--;),
                  result(count)
                  )
              )
              or (
              $past14DaysSymptoms: List() from accumulate(
                  Examination($dc.last14Days()<date, $pasDis: disease != null) from $examinations,
            	  init(List pasSymps = new ArrayList<>(); ),
            	  action(pasSymps.addAll($pasDis.getSymptoms());),
            	  result(pasSymps)
              )
              and
              Number(intValue >= 1) from accumulate(
                  Symptom(text == "Temperatura od 40 do 41"
                         || text == "Temperatura veca od 38"
                  ) from $past14DaysSymptoms,
            	  init(int count = 0;),
            	  action(count++;),
            	  result(count)
              )
              )
              or
              (
               $past: List() from accumulate(
                   Examination($dc.last21Days()<date, disease != null, $pastMedications: medications) from $examinations,
                   init(List $pastMedications = new ArrayList<>();),
                   action($pastMedications.addAll($pastMedications);),
                   result($pastMedications)
                        	)
                   and
                   Number(intValue >= 1) from accumulate(
                   Medication(medicationType==MedicationType.ANTIBIOTIC) from $past,
                   init(int count = 0;),
                   action(count++;),
                   result(count)
               )
               )
            )
	then
		modify($ex) {setDisease($d);}
		System.out.println("\"Akutna bubrezna povreda: Svi simptomi zadovoljeni.");
end